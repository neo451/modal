require "moon.all"
utils = {}

tsize = (t) ->
  size = 0
  for _ in pairs(t) 
    size = size + 1
  size

utils.compare = (rhs, lhs) ->
  if type(lhs) != type(rhs)
    return false
  if type(lhs) == "table"
    if tsize(lhs) != tsize(rhs)
      return false
    for k, v in pairs(lhs)
      equal = utils.compare(v, rhs[k])
      if not equal
        return false
  else
    return rhs == lhs
  return true

utils.type = (obj) -> is_object(obj) and obj\type! or type(obj)

utils.map = (items, func) -> [func(item) for item in * items]

utils.filter = (items, func) -> [item for item in *items when func(item)]

utils.fold = fold

utils.flatten = (t) ->
  flat = {}
  for value in *t
    if type(value) == "table"
      for value in *utils.flatten(value)
        table.insert flat, value
    else
        table.insert flat, value
  flat

return utils
